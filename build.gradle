/**
 * A high performance, asynchronous Java client to query the Orchestrate.io platform.
 */

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'

// FIXME temporary
    apply plugin: 'application'
    mainClassName = 'io.orchestrate.client.Main'
    dependencies.runtime group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.5'
    run {
        systemProperties [
            System.getProperty('org.slf4j.simpleLogger.defaultLogLevel', 'trace')
        ]
    }

def groovyVersion = 'groovy-2.1.8'

group   = 'io.orchestrate'
version = '0.0.1-SNAPSHOT'

plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    tasks.withType(Compile).all {
        // enable all warnings as errors
        options.compilerArgs = [
                '-Xlint:cast,deprecation,divzero,empty,unchecked,fallthrough,path,serial,finally,overrides,-options',
                '-Werror',
                '-XprintProcessorInfo'
        ]
        options.encoding = "UTF-8"
    }
}

repositories {
    mavenCentral()
}

configurations {
    provided
    testProvided { extendsFrom provided }
}

sourceSets {
    main { compileClasspath += configurations.provided }
    test { compileClasspath += configurations.testProvided }
}

dependencies {
    def slf4jVersion   = '1.7.5'
    def grizzlyVersion = '2.3.7'

    provided(
        [group: 'org.projectlombok', name: 'lombok', version: '1.12.2'],
        [group: 'com.google.code.findbugs', name: 'jsr305', version: '2.0.2']
    )
    compile(
        [group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion],
        [group: 'org.glassfish.grizzly', name: 'grizzly-http', version: grizzlyVersion],
        [group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.3.0-rc1']
    )
    testCompile(
        [group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.2.0-rc-2'],
        [group: 'org.spockframework', name: 'spock-core', version: '0.7-groovy-2.0']
    )
    testRuntime(
        [group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion]
    )
}

test {
    jvmArgs '-ea', '-Djava.awt.headless=true', '-Xms128m', '-Xmx512m', '-XX:MaxPermSize=512m'
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                   'Implementation-Version': project.version,
                   'Created-By': 'Gradle ' + gradle.gradleVersion,
                   'Build-Jdk': System.properties['java.version'],
                   'Manifest-Version': 1.0
    }
}

javadoc {
    classpath += configurations.provided
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true

        scopes.PROVIDED.plus += configurations.provided

        // enrich the IDE with Groovy support
        iml.withXml { provider ->
            provider.node.component.first().appendNode('orderEntry',
                    [type: 'library', name: groovyVersion, level: 'application'])
        }
    }

    project.ipr {
        withXml { provider ->
            // let the IDE know that this is a Git project
            provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
            // turn on annotation processing in the IDE
            provider.node.component.find { it.@name == 'CompilerConfiguration' }.annotationProcessing[0].replaceNode {
                annotationProcessing {
                    profile(default: true, name: 'Default', enabled: true) {
                        processorPath useClasspath: true
                    }
                }
            }
        }
    }
}

eclipse {
    classpath { plusConfigurations += configurations.provided }
}
